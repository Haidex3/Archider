import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Hyprland
import "../../../theme" as Theme

/*!
    A dynamic widget for displaying and switching between Hyprland workspaces.
    This component renders a horizontal row of workspace indicators
    based on the Hyprland workspaces.
*/
Item {
    implicitWidth: workspaceRow.implicitWidth
    implicitHeight: parent
    width: implicitWidth
    height: implicitHeight
    
    // Map as JavaScript object that defines custom display names for workspaces
    property var workspaceNames: {
        "1": "➊",
        "2": "➋",
        "3": "➌",
        "4": "➍",
        "5": "➎",
        "6": "➏",
        "7": "➐",
        "8": "➑",
        "9": "➒",
        "10": "➓",
        "11": "⓫"
    }

    
    // Show workspaces
    RowLayout {
        id: workspaceRow
        anchors.centerIn: parent
        
        Repeater {
            model: Hyprland.workspaces
            delegate: Rectangle {
                // Hide special workspaces
                visible: modelData.id > 0

                // Collapse non visible items
                width: visible ? 25 : 0
                height: visible ? 25 : 0
                
                color: "transparent"
                Layout.alignment: Qt.AlignVCenter
                
                // Workspaces colors & names implementation
                Text {
                    anchors.centerIn: parent
                    text: workspaceNames[modelData.id.toString()] || modelData.id
                    
                    // Color to each type of workspace (active & focus - active & noFocus - inactive)
                    // This is for multiple monitors
                    color: {
                        if (modelData.focused) {
                            return Theme.ThemeManager.color8 // focus
                        } else if (modelData.active) {
                            return Theme.ThemeManager.color6 // no-focus
                        } else {
                            return Theme.ThemeManager.color2
                        }
                    }
                    
                    font.pixelSize: Theme.ThemeManager.baseFontSize
                    
                }
                
                // Button border to active & focus workspace
                Rectangle {
                    id: bottomBorder
                    width: parent.width * 0.8
                    height: 2
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.bottom: parent.bottom
                    
                    // Color to each type of workspace (active & focus - active & noFocus - inactive)
                    // This is for multiple monitors
                    color: {
                        if (modelData.focused) {
                            return Theme.ThemeManager.color8 // focus
                        } else if (modelData.active) {
                            return Theme.ThemeManager.color6 // no-focus
                        } else {
                            return "transparent"
                        }
                    }
                }
            
                // Clickable
                MouseArea {
                    anchors.fill: parent
                    cursorShape: Qt.PointingHandCursor
                    onClicked: Hyprland.dispatch("workspace " + modelData.id)
                }
            }
        }
    }
}